plugins {
    id 'java'

    id 'me.bristermitten.pdm' version '0.0.30'
    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

group = 'com.nextplugins'
version = '1.0.0'

tasks.build.dependsOn('pdm', 'shadowJar')

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()

    // spigot

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    // libs

    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://repo.citizensnpcs.co/' }

    maven { url = 'https://jitpack.io/' }
}

dependencies {

    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'

    implementation 'com.github.HenryFabio:inventory-api:2.0.3'
    implementation 'com.github.HenryFabio:sql-provider:9561f20fd2'
    implementation 'com.github.HenryFabio:configuration-injector:main-SNAPSHOT'

    pdm 'com.github.HenryFabio:github-updater:main-SNAPSHOT'
    pdm 'com.github.SaiintBrisson:command-framework:1.1.0'
    pdm 'fr.minuskube:netherboard-bukkit:1.2.1'

    pdm "com.google.inject:guice:4.0"
    pdm "javax.inject:javax.inject:1"
    pdm "aopalliance:aopalliance:1.0"
    pdm "org.ow2.asm:asm:8.0.1"

    def lombok = 'org.projectlombok:lombok:1.18.16'
    compileOnly lombok
    annotationProcessor lombok

}

bukkit {
    main = 'com.nextplugins.testserver.core.NextTestServer'
    authors = ['Henry_Fabio', 'Eike', 'Gabriel Santos', 'Yuhtin']
    website = 'https://github.com/NextPlugins'
    version = "${project.version}"
    load = 'STARTUP'
}

shadowJar {
    archiveName("${project.name}-${project.version}.jar")
    relocate 'com.henryfabio.minecraft.inventoryapi', 'com.nextplugins.economy.libs.inventoryapi'
    relocate 'com.henryfabio.sqlprovider', 'com.nextplugins.economy.libs.sqlprovider'
    relocate 'com.henryfabio.minecraft.configinjector', 'com.nextplugins.economy.libs.configinjector'
}

compileJava {
    options.encoding('UTF-8')

    sourceCompatibility('1.8')
    targetCompatibility('1.8')
}
