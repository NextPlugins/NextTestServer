plugins {
    id 'java'

    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'

}

group = 'com.nextplugins'
version = '1.0.2'

tasks.build.dependsOn 'shadowJar'

repositories {
    mavenCentral()
    mavenLocal()

    // spigot

    maven { url = 'http://maven.elmakers.com/repository/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    // libs

    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://repo.citizensnpcs.co/' }

    maven { url = 'https://jitpack.io/' }

}

dependencies {

    compileOnly 'org.spigotmc:spigot:1.8.8-R0.1-SNAPSHOT'
    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'

    implementation 'com.github.HenryFabio.configuration-injector:bukkit:main-SNAPSHOT'
    implementation 'com.github.SaiintBrisson.command-framework:bukkit:1.2.0'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.9.0' // 3.0.0 or higher is for java 11+
    implementation 'fr.minuskube:netherboard-bukkit:1.2.1'

    // local dependency
    implementation 'com.github.eikefab.libs:yamladapter:1.1-SNAPSHOT'

    implementation 'com.github.HenryFabio:inventory-api:2.0.3'
    implementation 'com.github.HenryFabio:sql-provider:9561f20fd2'

    implementation "com.google.inject:guice:4.0"
    implementation "javax.inject:javax.inject:1"
    implementation "aopalliance:aopalliance:1.0"
    implementation "org.ow2.asm:asm:8.0.1"

    def lombok = 'org.projectlombok:lombok:1.18.16'
    compileOnly lombok
    annotationProcessor lombok

}

bukkit {
    main = 'com.nextplugins.testserver.core.NextTestServer'
    authors = ['Henry_Fabio', 'Eike', 'Gabriel Santos', 'Yuhtin']
    website = 'https://github.com/NextPlugins'
    version = "${project.version}"
    depend = ['PlaceholderAPI']
    loadBefore = ['NextUpdater']
    load = 'POSTWORLD'
}

shadowJar {
    archiveName "${project.name}-${project.version}.jar"

    relocate 'com.github.eikefab.libs.yamladapter', 'com.nextplugins.testserver.core.libs.yamladapter'

    relocate 'com.henryfabio.minecraft.configinjector', 'com.nextplugins.testserver.core.libs.configinjector'
    relocate 'com.henryfabio.minecraft.inventoryapi', 'com.nextplugins.testserver.core.libs.inventoryapi'

    relocate 'com.henryfabio.sqlprovider', 'com.nextplugins.testserver.core.libs.sqlprovider'
    relocate 'com.zaxxer.hikari', 'com.nextplugins.testserver.core.libs.hikari'

    relocate 'fr.minuskube.netherboard', 'com.nextplugins.testserver.core.libs.netherboard'

    relocate 'com.github.benmanes.caffeine', 'com.nextplugins.testserver.core.libs.caffeine'

    relocate 'me.saiintbrisson.minecraft.command', 'com.nextplugins.testserver.core.libs.command.common'
    relocate 'me.saiintbrisson.bukkit.command', 'com.nextplugins.testserver.core.libs.command.bukkit'

    // guice
    relocate 'com.google.common', 'com.nextplugins.testserver.core.libs.guice.common'
    relocate 'com.google.inject', 'com.nextplugins.testserver.core.libs.guice.inject'
    relocate 'com.google.thirdparty', 'com.nextplugins.testserver.core.libs.guice.thirdparty'
    relocate 'javax.inject', 'com.nextplugins.testserver.core.libs.guice.inject.javax'

    relocate 'com.google.errorprone.annotations', 'com.nextplugins.testserver.core.libs.annotations'

}

compileJava {
    options.encoding 'UTF-8'

    sourceCompatibility '1.8'
    targetCompatibility '1.8'
}
