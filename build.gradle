plugins {
    id 'java'

    id 'me.bristermitten.pdm' version '0.0.30'
    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'

}

group = 'com.nextplugins'
version = '1.0.1'

tasks.build.dependsOn('pdm', 'shadowJar')

repositories {
    mavenCentral()
    mavenLocal()

    // spigot

    maven { url = 'http://maven.elmakers.com/repository/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    // libs

    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://repo.citizensnpcs.co/' }

    maven { url = 'https://jitpack.io/' }

}

dependencies {

    compileOnly 'org.spigotmc:spigot:1.8.8-R0.1-SNAPSHOT'
    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'

    implementation 'com.github.HenryFabio.configuration-injector:common:main-SNAPSHOT'
    implementation 'com.github.HenryFabio.configuration-injector:bukkit:main-SNAPSHOT'
    implementation 'fr.minuskube:netherboard-bukkit:1.2.1'

    // local dependency
    implementation 'com.github.eikefab.libs:yamladapter:1.1-SNAPSHOT'

    pdm 'com.github.HenryFabio:inventory-api:2.0.3'
    pdm 'com.github.HenryFabio:sql-provider:9561f20fd2'

    pdm 'com.github.SaiintBrisson.command-framework:bukkit:1.2.0'
    pdm 'com.github.SaiintBrisson.command-framework:shared:1.2.0'

    pdm 'com.github.ben-manes.caffeine:caffeine:2.9.0' // 3.0.0 or higher is for java 11+

    pdm "com.google.inject:guice:4.0"
    pdm "javax.inject:javax.inject:1"
    pdm "aopalliance:aopalliance:1.0"
    pdm "org.ow2.asm:asm:8.0.1"

    def lombok = 'org.projectlombok:lombok:1.18.16'
    compileOnly lombok
    annotationProcessor lombok

}

bukkit {
    main = 'com.nextplugins.testserver.core.NextTestServer'
    authors = ['Henry_Fabio', 'Eike', 'Gabriel Santos', 'Yuhtin']
    website = 'https://github.com/NextPlugins'
    version = "${project.version}"
    depend = ['PlaceholderAPI']
    loadBefore = ['NextUpdater']
    load = 'POSTWORLD'
}

shadowJar {
    archiveName("${project.name}-${project.version}.jar")
    relocate 'com.henryfabio.minecraft.configinjector', 'com.nextplugins.testserver.core.libs.configinjector'
    relocate 'fr.minuskube.netherboard', 'com.nextplugins.testserver.core.libs.netherboard'
}

compileJava {
    options.encoding('UTF-8')

    sourceCompatibility('1.8')
    targetCompatibility('1.8')
}
